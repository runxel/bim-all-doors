
! ######################################
!
! BIM-all-doors - DOOR SYSTEM
!
! Module: Frame Macro
!
! Manufacturer: common
! Linie: common
!
! © 2018 BY DORMAKABA / WRITTEN BY JOCHEN SÜHLO
! 
! ######################################

! =============================================================================
! GLOBAL SETTINGS
! =============================================================================

! »»» Arrays

DIM clr1[], clr2[][], UI_page_subs[]
DIM UI_page_names[], UI_page_icons[], UI_page_type[]

! »»» Constants

tlr = 0.00001 	!»» accurancy
version = 1.57 	!»» Objektversion
txt = ""	  	!»»  Text für alles
last_change_macro = "16.04.19"

! »»» Localization

sprache = language  !»»  Deutsch


! »»» Common keywords

IF sprache = 1 THEN
	commonKey = "unspezifiziert" 
	naKey = "undefiniert" 
	noKey = "nein" 
	yesKey = "ja"
	ELSE
	commonKey = "unspecific"
	naKey = "common"
	noKey = "no"
	yesKey = "yes"
	ENDIF
	

! ----------------------------------- INDEX NAMES OF ARRAY PARAMETERS (start)

! --------- PRODUCT PARAMETERS

! --------- profile[i][…]
	dki_Typ = 1
	dki_Name = 2
! --------- abmessung[i][…]
	dki_Breite = 1
	dki_Tiefe = 2
! --------- DEFAULT VALUE ARRAYS

! --------- dk_folding_dimensions[i][…]
	dki_Falz1 = 1
	dki_Falz2 = 2
! --------- dk_cont_pens[i][…]
	dka_handle_penC2 = 1
	dka_handle_penC3 = 2
	dka_frame_penC2 = 3
	dka_frame_penC3 = 4
	dka_panel_penC2 = 5
	dka_panel_penC3 = 6
	dka_opline2D = 7
	dka_oplineSA = 8
	dka_opline3D = 9
	dka_topview_penC2 = 10
	dka_penInvisible = 11
	dka_closer_penSA = 12
	dka_closer_pen3D = 13
	dka_grip_penC2 = 14
	dka_grip_penC3 = 15
! --------- dk_fill_pens[i][…]
	dka_handle_penF = 1
	dka_frame_penF = 2
	dka_side1_penF = 3
	dka_side2_penF = 4
	dka_panel_penF = 5
	dka_panelfill1_penF = 6
	dka_panelfill2_penF = 7
	dka_panelfill3_penF = 8
	dka_panel2fill1_penF = 9
	dka_panel2fill2_penF = 10
	dka_panel2fill3_penF = 11
	dka_topview_penF = 12
	dka_fan_penF = 13
	dka_gap_filling_penF = 14
	dka_grip_penF = 15
! --------- dk_bkg_pens[i][…]
	dka_handle_penB = 1
	dka_frame_penB = 2
	dka_side1_penB = 3
	dka_side2_penB = 4
	dka_panel_penB = 5
	dka_panelfill1_penB = 6
	dka_panelfill2_penB = 7
	dka_panelfill3_penB = 8
	dka_panel2fill1_penB = 9
	dka_panel2fill2_penB = 10
	dka_panel2fill3_penB = 11
	dka_topview_penB = 12
	dka_fan_penB = 13
	dka_gap_filling_penB = 14
	dka_grip_penB = 15
! --------- dk_fills[i][…]
	dka_handle_fill = 1
	dka_frame_fill = 2
	dka_side1_fill = 3
	dka_side2_fill = 4
	dka_panel_fill = 5
	dka_panelfill1_fill = 6
	dka_panelfill2_fill = 7
	dka_panelfill3_fill = 8
	dka_panel2fill1_fill = 9
	dka_panel2fill2_fill = 10
	dka_panel2fill3_fill = 11
	dka_topview_fill = 12
	dka_fan_fill = 13
	dka_gap_filling_fill = 14
	dka_grip_fill = 15
! --------- dk_materials[i][…]
	dka_handle_mat = 1
	dka_plate_mat = 2
	dka_frame_mat_BS = 3
	dka_frame_mat_BGS = 4
	dka_side1_mat_BS = 5
	dka_side1_mat_BGS = 6
	dka_side2_mat_BS = 7
	dka_side2_mat_BGS = 8
	dka_fan_mat_BS = 9
	dka_fan_mat_BGS = 10
	dka_panel_mat_BS = 11
	dka_panel_mat_BGS = 12
	dka_panelfill1_mat_BS = 13
	dka_panelfill1_mat_BGS = 14
	dka_panelfill2_mat_BS = 15
	dka_panelfill2_mat_BGS = 16
	dka_panelfill3_mat_BS = 17
	dka_panelfill3_mat_BGS = 18
	dka_panel2_mat_BS = 19
	dka_panel2_mat_BGS = 20
	dka_panel2fill1_mat_BS = 21
	dka_panel2fill1_mat_BGS = 22
	dka_panel2fill2_mat_BS = 23
	dka_panel2fill2_mat_BGS = 24
	dka_panel2fill3_mat_BS = 25
	dka_panel2fill3_mat_BGS = 26
	dka_panel_bandings_mat = 27
	dka_panel2_bandings_mat = 28
	dka_hinge_mat = 29
	dka_closer_mat = 30
	dka_automation_mat = 31
	dka_grip_mat_BS1 = 32
	dka_grip_mat_BS2 = 33
	dka_grip_mat_BGS1 = 34
	dka_grip_mat_BGS2 = 35
! --------- dk_linetype[i][…]
	dka_invisible = 1

! ----------------------------------- INDEX NAMES OF ARRAY PARAMETERS (end)

! ----------------------------------- MULTILINGUAL TEXT [ECD FRAME] (start)

! --------- RESET array variables
	DIM uiTXT[]

! --------- User Interface - UI_xxxFIELD text (Deutsch)
	IF language = 1 THEN
		uiTXT[1] = "ZARGE - Einstellungen"
		uiTXT[2] = " ... Aufteilung der Seitenfelder / Oberlicht"
		uiTXT[3] = " ... Horizontale Abmessungen"
		uiTXT[4] = " ... Vertikale Abmessungen"
		uiTXT[5] = " ... Material + Schraffuren + Stifte"
		ENDIF
! --------- User Interface - UI_xxxFIELD text (Englisch)
	IF language = 4 THEN
		uiTXT[1] = "FRAME - settings"
		uiTXT[2] = " ... divisions for sidelights + fanlight"
		uiTXT[3] = " ... horizontal dimensions"
		uiTXT[4] = " ... vertical dimensions"
		uiTXT[5] = " ... material + hatches + pens"
		ENDIF

! ----------------------------------- MULTILINGUAL TEXT (end)

! »»» Dicke "Folien"tür
symbolicTHK = 0.001

! »»» Effektive Wandstärke (ohne Bekleidung)  
wallThicknessEffective = WALL_THICKNESS + (WALL_THICKNESS < tlr) * 0.365 - wall_shell_thk_1 - wall_shell_thk_2

! »»» Seitenfelder?
IF bs_door_sidelight = "L" OR bs_door_sidelight = "LR" THEN gs_sidelight_left  = 1 ELSE gs_sidelight_left = 0
IF bs_door_sidelight = "R" OR bs_door_sidelight = "LR" THEN gs_sidelight_right = 1 ELSE gs_sidelight_right = 0

! »»» Oberlicht oder Oberblende?
oberLichtBlende = gs_door_transom * (1 + (bs_inner_frame = 2))

! »»» geringes LoI, einheitliches Material

IF dk_LoI < 5 THEN
	IF oberLichtBlende THEN
		unifyingMaterial = dka_fan_mat_BS
		ELSE
		IF gs_sidelight_left THEN
			unifyingMaterial = dka_side2_mat_BS
			ELSE
			IF gs_sidelight_right THEN
				unifyingMaterial = dka_side1_mat_BS
				ELSE
				unifyingMaterial = 0
				ENDIF
			ENDIF
		ENDIF
	ELSE
	unifyingMaterial = 0
	ENDIF


! ----------------------------------------------------------------[ parameter limitations ]

! »»» Wertebereiche Rahmenbreite
IF gs_sidelight_right = 0 AND gs_sidelight_left = 0 THEN
	_range_frame_width = (ac_unit_width - 0.25) /2
	ELSE
	_range_frame_width = (ac_unit_width - ac_egress_width - 0.01)/(2 + gs_sidelight_right + gs_sidelight_left)
	ENDIF

! »»» Auf Sinnhaftigkeit einer Begrenzung der Rahmenstärke prüfen:
IF dk_frametype = "BZ" OR dk_frametype = "UZ" THEN
	_range_frame_thk = wallThicknessEffective + gs_ub_st2 * 2
	ELSE
	IF dk_frametype = "EZ" THEN
		_range_frame_thk = wallThicknessEffective + gs_ub_st2
		ELSE
		_range_frame_thk = 9999
		ENDIF
	ENDIF

! »»»Minimalabmessungen Profile

minFrameWidth	= gs_vb + 0.001
minFrameThk		= MAX(0.002, gs_vt + 0.001)
minFrameViewWidth = MAX(gs_frame_width_left, gs_frame_width_right) - gs_vb


! ----------------------------------------------------------------[ Rahmen-/Profil-/Zargenaußenmaße ]

! Automatisiert erzeugte Zargenstärken ermitteln
! bz_typ_einzug: 0: "Wie Wandstärke"; 1: "Eingezogen"

! IF dk_different_frame_thk THEN frameThickness = dk_frame_thk_EZ ELSE frameThickness = gs_frame_thk

IF dk_frametype = "UZ" THEN

	frameThickness		= wallThicknessEffective + gs_ub_st2 * 2   ! = dk_frame_thk_EZ
	RimOpening			= frameThickness - gs_ub_st2 * 2 
	frameThicknessUpper	= frameThickness   ! = dk_frame_thk_EZ  ! Bei UZ ist oberes Randprofil immer gleich seitl. Rand

	ELSE

	IF dk_frametype = "BZ" THEN

		IF bz_typ_einzug = 1 THEN
			frameThickness	= bz_tiefe
			ELSE
			frameThickness	= wallThicknessEffective + gs_ub_st2 * 2
			ENDIF

		RimOpening			= frameThickness - gs_ub_st2 * 2   ! MW gibt es bei BZ eigentlich nicht!!
		frameThicknessUpper	= frameThickness  ! Bei BZ ist oberes Randprofil immer gleich seitl. Rand

		ELSE

		IF dk_frametype = "EZ" THEN

			frameThickness		= dk_frame_thk_EZ
			RimOpening			= frameThickness - gs_ub_st2   ! MW gibt es bei EZ eigentlich nicht!!
			frameThicknessUpper	= frameThickness  ! so definiert, sonst wäre es dk_frame_thk_upper

			ELSE   ! dk_frametype = "RR"

			frameThickness		= dk_frame_thk_EZ  !gs_frame_thk
			RimOpening			= frameThickness
			frameThicknessUpper	= frameThickness  ! so definiert, sonst wäre es dk_frame_thk_upper
			
			ENDIF 

		ENDIF 

	ENDIF


! ---------------------------------------------------------------------- [ unique frame sizes]

!IF dk_LOD < 350 THEN
!	gs_different_framewidth = 0
!	dk_different_frame_thk = 0
!	gs_tolerance_left = 0
!	gs_tolerance_right = 0
!ENDIF

IF dk_different_frame_thk THEN

	_frame_thk_upper 		= dk_frame_thk_upper
	_frame_thk_transom 		= dk_frame_thk_transom !- dk_design_folding_depth
	_frame_thk_bottom 		= dk_frame_thk_bottom
	_frame_thk_post 		= dk_frame_thk_post
	_frame_thk_EZ			= dk_frame_thk_EZ
	_post_thk_upper			= MIN(_frame_thk_post, _frame_thk_transom)

	ELSE

	IF dk_frametype = "BZ"  OR dk_frametype = "UZ" OR dk_frametype = "EZ"  THEN
		! Bei inneren Profilen weniger Türzargentiefe um Zierfalztiefe
		_tt_plus = - dk_design_folding_depth
		ELSE	
		_tt_plus = 0
		ENDIF

	_frame_thk_upper 		= frameThickness + _tt_plus
	_frame_thk_transom 		= frameThickness + _tt_plus
	_frame_thk_bottom 		= frameThickness + _tt_plus
	_frame_thk_post 		= frameThickness + _tt_plus
	_frame_thk_EZ			= frameThickness !+ _tt_plus
	_post_thk_upper			= frameThickness + _tt_plus

	ENDIF


IF gs_different_framewidth THEN

	_framewidth_left 		= gs_frame_width_left
	_framewidth_right 		= gs_frame_width_right
	_framewidth_post_left 	= gs_frame_width_left_2
	_framewidth_post_right 	= gs_frame_width_right_2 

	_frame_width_upper		= gs_frame_width_upper
	_frame_width_transom	= gs_frame_width_transom
	_framewidth_post_upper 	= _frame_width_transom
	_frame_width_bottom_sl	= gs_frame_width_bottom_sl

	ELSE

	_framewidth_left 		= gs_frame_width
	_framewidth_right 		= gs_frame_width
	_framewidth_post_left 	= gs_frame_width
	_framewidth_post_right 	= gs_frame_width
	_framewidth_post_upper 	= gs_frame_width 
	_frame_width_upper		= gs_frame_width
	_frame_width_transom	= gs_frame_width
	_frame_width_bottom_sl = gs_frame_width

	ENDIF	

IF gs_sidelight_left THEN
	IF gs_sidelight_right THEN
		offsetEgressCenter = ( gs_sidelight_width_left - gs_sidelight_width_right ) / 2
		ELSE
		offsetEgressCenter = ( gs_sidelight_width_left - _framewidth_right ) / 2
		ENDIF
	ELSE
	IF gs_sidelight_right THEN
		offsetEgressCenter = ( _framewidth_left - gs_sidelight_width_right ) / 2
		ELSE
		offsetEgressCenter = ( _framewidth_left - _framewidth_right ) / 2
		ENDIF
	ENDIF

! Versatz bis zumr Mitte der Zarge
offsetFrameCenter = ( gs_tolerance_left - gs_tolerance_right ) / 2

! Versatz bis Zargenoberfläche
offsetFrameSurfaceBS = offsetWallSurfaceBS + wall_shell_thk_1 + bz_einzug * (dk_frametype = "BZ" AND bz_typ_einzug = 1) + bz_einzug * (dk_frametype = "RR")

! ---------------------------------------------------------------------- !

! F e s t l e g u n g   d e r   A b m e s s u n g e n   E i n z e l p f o s t e n   O b e r l i c h t

dk_post_width_upper = _frame_width_transom 
dk_post_thk_upper = MIN(_frame_thk_post, _frame_thk_transom) 


! =============================================================================
! Zargenaußenmaße
! =============================================================================

_outside = 0
_inside = 0

! --------- Abmessungen nach LoD

IF dk_LOD < 400 THEN 
	frame_out_dims_x = ac_wallhole_width   					! wird vom Hauptobjekt übergeben
	frame_out_dims_z = ac_unit_height + gs_tolerance_upper  	! wird vom Hauptobjekt übergeben
	clearingFrameCenter = offsetFrameCenter
	_framewidth_left  = _framewidth_left  + gs_tolerance_left
	_framewidth_right = _framewidth_right + gs_tolerance_right
	! _tt_plus = 0
	ELSE
	frame_out_dims_x = ac_unit_width  		! wird vom Hauptobjekt übergeben
	frame_out_dims_z = ac_unit_height 		! wird vom Hauptobjekt übergeben
	clearingFrameCenter  = 0
	ENDIF

frame_out_dims_y = RimOpening  ! nicht frameThickness  / -> wird oben berechnet


! =============================================================================
! Geometrien für Außenkommunikation
! =============================================================================

! Montageplatz Obentürschließer / Automation auf Zarge/Wand

IF oberLichtBlende = 0 THEN

	! #### kein Kämpfer oder Oberblende, oberes Zargenprofil

	! ZARGENSPIEGELBREITE OBEN an Bandseite
	IF dk_frametype = "UZ" OR dk_frametype = "EZ"  THEN
		facingLintelBS = bs_spiegel_br_ob_bs - bs_leaf_oversize
		ELSE   ! = "BZ"   = "RR"
		facingLintelBS = gs_frame_width_upper - gs_vb - bs_leaf_oversize
		ENDIF

	! ZARGENSPIEGELBREITE OBEN an Bandgegenseite
	IF dk_frametype = "UZ"  THEN
		facingLintelBGS = bs_spiegel_br_ob_bgs
		ELSE   ! = "BZ"   = "RR"   = "EZ"
		facingLintelBGS = gs_frame_width_upper
		ENDIF

	! ABSTAND ZARGENSPIEGELOBERFLÄCHEN Bandseite zu Bandgegenseite
	offsetFrameSurfaceBGS = dk_frame_thk_upper

	! ABSTAND MONTAGEBEZUGSEBENE ZU WANDOBERFLÄCHE (ohne Bekleidung) an Bandseite
	IF dk_frametype = "UZ" OR dk_frametype = "EZ"  THEN
		frame2wallSurfaceBS  = - gs_ub_st2    !+ wall_shell_thk_1
		ELSE
		IF dk_frametype = "BZ" THEN
			frame2wallSurfaceBS  = - gs_ub_st2 + bz_einzug * bz_typ_einzug    !+ wall_shell_thk_1
			ELSE   ! = "RR"
			frame2wallSurfaceBS  = bz_einzug    !+ wall_shell_thk_1
			ENDIF
		ENDIF

	! ABSTAND MONTAGEBEZUGSEBENE ZU WANDOBERFLÄCHE (ohne Bekleidung) an Bandgegenseite
	frame2wallSurfaceBGS = wallThicknessEffective - frame2wallSurfaceBS - offsetFrameSurfaceBGS    !+ wall_shell_thk_2

	ELSE

	IF oberLichtBlende = 1 THEN

		! #### mit Kämpfer

		! ZARGENSPIEGELBREITE OBEN Band(gegen)seite
		facingLintelBS =  gs_frame_width_transom - gs_vb - bs_leaf_oversize
		facingLintelBGS = gs_frame_width_transom

		! ABSTAND ZARGENSPIEGELOBERFLÄCHEN Bandseite zu Bandgegenseite
		offsetFrameSurfaceBGS =	dk_frame_thk_transom

		! ABSTAND MONTAGEBEZUGSEBENE ZU WANDOBERFLÄCHE (ohne Bekleidung) an Bandgegenseite
		IF dk_frametype = "UZ" OR dk_frametype = "EZ"  THEN
			frame2wallSurfaceBS  = - gs_ub_st2    !+ wall_shell_thk_1
			ELSE
			IF dk_frametype = "BZ" THEN
				frame2wallSurfaceBS  = - gs_ub_st2 + bz_einzug * bz_typ_einzug    !+ wall_shell_thk_1
				ELSE   ! = "RR"
				frame2wallSurfaceBS  = bz_einzug    !+ wall_shell_thk_1
				ENDIF
			ENDIF

		ELSE

		! #### mit Oberblende

		! "ZARGENSPIEGEL"BREITE OBEN Band(gegen)seite
		facingLintelBS =  bs_FrameRebate_height + bs_leaf_oversize - ac_egress_height
		facingLintelBGS = bs_FrameRebate_height - gs_vb - ac_egress_height

		! ABSTAND MONTAGEBEZUGSEBENEN Bandseite zu Bandgegenseite
		offsetFrameSurfaceBGS =	gs_leaf_thk - bs_leaf_overhang   ! -offsetHingePanelSurfaceBS ???

		! ABSTAND MONTAGEBEZUGSEBENE ZU WANDOBERFLÄCHE (ohne Bekleidung) je an Band(gegen)seite
		IF dk_frametype = "UZ" OR dk_frametype = "EZ"  THEN
			frame2wallSurfaceBS  = - gs_ub_st2 - bs_leaf_overhang   !+ wall_shell_thk_1
			ELSE
			IF dk_frametype = "BZ" THEN
				frame2wallSurfaceBS = - gs_ub_st2 - bs_leaf_overhang + bz_einzug * bz_typ_einzug    !+ wall_shell_thk_1
				ELSE   ! = "RR"
				frame2wallSurfaceBS = bz_einzug - bs_leaf_overhang    !+ wall_shell_thk_1
				ENDIF
			ENDIF

		ENDIF

	! ABSTAND MONTAGEBEZUGSEBENE ZU WANDOBERFLÄCHE (ohne Bekleidung) an Bandgegenseite
	frame2wallSurfaceBGS = wallThicknessEffective - frame2wallSurfaceBS - offsetFrameSurfaceBGS    !+ wall_shell_thk_2

	ENDIF

! Abstand obere Bezugskante (OK Türblatt) zu OK Zargenspiegel bei UZ / EZ
! IF dk_frametype = "UZ" OR dk_frametype = "EZ"  THEN ancor2frametopBS = facingLintelBS ELSE ancor2frametopBS = -999
! IF dk_frametype = "UZ" THEN ancor2frametopBGS = facingLintelBGS ELSE ancor2frametopBGS = -999


! =============================================================================
! UI-Seiten
! =============================================================================

!  UI_page_type =  -2 > sub of ROOT, -1 > sub of last, 1 > sub of first macro page

seiten = 1
IF dk_LOI > 4 THEN
	UI_page_names[seiten] = uiTXT[1] + " (" + frame_system + ")"
	UI_page_icons[seiten] = "dk_title_frame.png"
	UI_page_type[seiten]  = -1
	UI_page_subs[seiten] = 2500
	ELSE
	UI_page_names[seiten] = uiTXT[1] + " (" + frame_system + ")"
	UI_page_icons[seiten] = "dk_title_frame.png"
	UI_page_type[seiten]  = -1
	UI_page_subs[seiten] = 2600
	ENDIF

! »»» A U F T E I L U N G
IF dk_LOI = dk_LOI THEN  
	seiten = seiten+1
	UI_page_names[seiten] = uiTXT[2]  ! "... Aufteilung der Seitenfelder / Oberlicht"
	UI_page_icons[seiten] = "dk_title_segments.png"
	UI_page_type[seiten]  = 1
	UI_page_subs[seiten] = 2700
	ENDIF

! »»» UI-Seiten nach Level of Information
IF dk_LOI > 4 AND dk_frametype = "BZ" THEN    !Blockzarge
	seiten = seiten+1
	UI_page_names[seiten] = uiTXT[3]  ! "... Horizontale Abmessungen"
	UI_page_icons[seiten] = "dk_title_sizeH.png"
	UI_page_type[seiten]  = 1
	UI_page_subs[seiten] = 3000
	seiten = seiten+1
	UI_page_names[seiten] = "... Vertikale Abmessungen"
	UI_page_icons[seiten] = "dk_title_sizeV.png"
	UI_page_type[seiten]  = 1
	UI_page_subs[seiten] = 3100
	ENDIF

IF dk_LOI > 4 AND dk_frametype = "EZ" THEN    !Eckzarge
	seiten = seiten+1
	UI_page_names[seiten] = uiTXT[3]  ! "... Horizontale Abmessungen"
	UI_page_icons[seiten] = "dk_title_sizeH.png"
	UI_page_type[seiten]  = 1
	UI_page_subs[seiten] = 3500
	seiten = seiten+1
	UI_page_names[seiten] = uiTXT[4]  ! "... Vertikale Abmessungen"
	UI_page_icons[seiten] = "dk_title_sizeV.png"
	UI_page_type[seiten]  = 1
	UI_page_subs[seiten] = 3600
	ENDIF

IF dk_LOI > 4 AND dk_frametype = "UZ" THEN    !Umfassungszarge
	seiten = seiten+1
	UI_page_names[seiten] = uiTXT[3]  ! "... Horizontale Abmessungen"
	UI_page_icons[seiten] = "dk_title_sizeH.png"
	UI_page_type[seiten]  = 1
	UI_page_subs[seiten] = 4000
	seiten = seiten+1
	UI_page_names[seiten] = uiTXT[4]  ! "... Vertikale Abmessungen"
	UI_page_icons[seiten] = "dk_title_sizeV.png"
	UI_page_type[seiten]  = 1
	UI_page_subs[seiten] = 4100
	ENDIF

IF dk_LOI > 4 AND dk_frametype = "RR" THEN    !Rohrrahmenzarge
	seiten = seiten+1
	UI_page_names[seiten] = uiTXT[3]  ! "... Horizontale Abmessungen"
	UI_page_icons[seiten] = "dk_title_sizeH.png"
	UI_page_type[seiten]  = 1
	UI_page_subs[seiten] = 4500
	seiten = seiten+1
	UI_page_names[seiten] = uiTXT[4]  ! "... Vertikale Abmessungen"
	UI_page_icons[seiten] = "dk_title_sizeV.png"
	UI_page_type[seiten]  = 1
	UI_page_subs[seiten] = 4600
	ENDIF

! »»» M A T E R I A L S E I T E
IF dk_LOI > 4 THEN  
	seiten = seiten+1
	UI_page_names[seiten] = uiTXT[5]  ! "... Material + Schraffuren + Stifte"
	UI_page_icons[seiten] = "dk_title_frame_mat.png"
	UI_page_type[seiten]  = 1
	UI_page_subs[seiten] = 4700
	ENDIF

! Makroende, wenn nur Voranfrage
IF macro_runtype = 1   THEN END UI_page_type   ! »»» return UI page hirarchy
IF macro_runtype = 2   THEN END UI_page_names  ! »»» return UI page names
IF macro_runtype = 4   THEN END UI_page_icons  ! »»» return UI page picts


! =============================================================================
! Zargenbekleidung auf Gehrung oder auf Stoß ?
! =============================================================================

_oben_gehrung = 0
_oben_laenger = 0

IF gs_different_framewidth THEN
	IF ABS(gs_frame_width_right - gs_frame_width_upper) > tlr OR ABS(gs_frame_width_right - gs_frame_width_upper) > tlr THEN
		_oben_gehrung = 0
		ELSE
		_oben_gehrung = 1
		ENDIF
	ELSE
	_oben_gehrung = 1
	ENDIF

IF ABS(bs_spiegel_br_bs - bs_spiegel_br_ob_bs) > tlr OR ABS(bs_spiegel_br_bgs - bs_spiegel_br_ob_bgs) > tlr THEN
	_oben_gehrung = 0
	IF dk_LOD > 399 AND (dk_frametype = "UZ" OR dk_frametype = "EZ") THEN _oben_laenger = 1
	ENDIF

IF dk_frametype = "BZ" OR dk_frametype = "RR" THEN gs_ub_st1 = 0
IF dk_frametype = "RR" THEN gs_ub_st2 = 0


! =============================================================================
! Plausibilitätsprüfung
! =============================================================================

DIM errorQ[]
errorI = 0
errorQ[1] = ""

! »»» Fehlermeldungen

IF GLOB_PREVIEW_MODE THEN

	IF (( bs_frame_material = "KU" AND bs_panel_material # "KU") OR \
		( bs_panel_material = "KU" AND bs_frame_material # "KU") OR \
		( bs_frame_material = "HO" AND bs_panel_material # "HO") OR \
		( bs_frame_material = "ST" AND bs_panel_material = "AL") OR \
		( bs_frame_material = "AL" AND bs_panel_material = "ST")) AND bs_door_leafs > 0 THEN
		errorI = errorI+1 : errorQ[errorI] = "Zargenmaterial passt nicht zum Türblatt."
		ENDIF

	IF  ( dk_frametype = "RR" AND (bs_frame_material # "KU" AND  bs_frame_material # "ST" AND bs_frame_material # "AL") ) OR \
		( dk_frametype = "UZ" AND bs_frame_material = "KU") THEN
		errorI = errorI+1 : errorQ[errorI] = "Zargenmaterial passt nicht zum Zargentyp."
		ENDIF
		
	ENDIF

! sts = 1+2+8 : GOSUB "debuginfo"
! errorI = errorI+1 : errorQ[errorI] = "FRAME: "+txt

! =============================================================================
! ENDE INITIALISIERUNG
! =============================================================================

IF macro_runtype = 256 THEN END offsetFrameSurfaceBGS,facingLintelBS, facingLintelBGS,
								frame2wallSurfaceBS, frame2wallSurfaceBGS, errorQ  ! »»» Fehlermeldung
IF macro_runtype = 512 THEN END offsetFrameSurfaceBGS,  ! »»» nur Umgebungswerte zurück
								facingLintelBS, facingLintelBGS,
								frame2wallSurfaceBS, frame2wallSurfaceBGS

! ------------------------------------------------------------------------[ ... ]

GOTO "masterend"


! =============================================================================
! =============================================================================
!
! SUBROUTINES
!
! =============================================================================
! =============================================================================

! Errorcontent:
"debuginfo":
	IF BITTEST(sts,0) THEN txt = "Falscher Wandanschluss " ELSE txt=""
	IF BITTEST(sts,1) THEN txt = txt + STRSUB(", ",1,2*(txt#"")) + "Anschlag"
	IF BITTEST(sts,2) THEN txt = txt + STRSUB(", ",1,2*(txt#"")) + "Nicht bündig mit Zarge"
	IF BITTEST(sts,3) THEN txt = txt + STRSUB(", ",1,2*(txt#"")) + "Leibungstiefe"
	IF BITTEST(sts,4) THEN txt = txt + STRSUB(", ",1,2*(txt#"")) + "Türbreite"
	IF BITTEST(sts,5) THEN txt = txt + STRSUB(", ",1,2*(txt#"")) + "sonstiges"
	IF txt # "" THEN txt = " ("+txt+")"
	RETURN

"masterend":


