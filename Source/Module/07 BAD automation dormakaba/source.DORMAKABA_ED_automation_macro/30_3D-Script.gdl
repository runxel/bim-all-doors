IF NOT( BITTEST(macro_runtype,5) ) OR \
	(stand AND ACT<3) THEN END

! PROJECT2 4,270,2

! ----------------------------------------------------------------- [ Grunddefinition der Variablen ]

! Status der Kanten
msk = 15
msk2 = 15+64

! »»» Öffnungswinkel und Stifte
IF GLOB_VIEW_TYPE = 4 OR GLOB_VIEW_TYPE = 5 THEN
	
	! »»» Schnitt/Ansicht
	PEN dk_cont_pens[dka_automation_penSA] 

	IF stand THEN
		open_leaf = dk_open_leaf[dka_leaf2_angleSA]
		ELSE
		open_leaf = dk_open_leaf[dka_leaf_angleSA]
		ENDIF

	ELSE

	! »»» 3D
	PEN dk_cont_pens[dka_automation_penC3]
	IF stand THEN
		open_leaf = dk_open_leaf[dka_leaf2_angle3D]
		ELSE
		open_leaf = dk_open_leaf[dka_leaf_angle3D]
		ENDIF

	ENDIF


! =============================================================================
! Ausgabe Schliesser
! =============================================================================

MATERIAL dk_materials[dka_automation_mat]

! IF dk_LOD < 101 THEN GOSUB "LoD100"
! IF dk_LOD > 100 AND dk_LOD < 201 THEN GOSUB "LoD200"
! IF dk_LOD > 200 AND dk_LOD < 301 THEN GOSUB "LoD300"
IF dk_LOD > 300 AND dk_LOD < 351 THEN GOSUB "LoD350"
IF dk_LOD > 350 AND dk_LOD < 401 THEN GOSUB "LoD400"
IF dk_LOD > 400 THEN GOSUB "LoD500"

! ------------------------------------------------------------------------- [ ENDE ]

"ende":
END htspt


! =============================================================================
! =============================================================================
!
! SUBROUTINES
!
! =============================================================================
! =============================================================================

"LoD100":
"LoD200":
"LoD300":
	! »»» Keine Darstellung
	RETURN

"LoD350":
	! »»» Nur beim Editieren darstellen
	IF NOT(GLOB_FEEDBACK_MODE) THEN RETURN

"LoD400":
"LoD500":

	! »»» Eigenschaften standardisieren
	IF stand THEN
		IF ACT # 3 AND ACT # 5 THEN
			GOSUB "copyMain"
			schere = ( strstr(bs_automation_model2,"GST") > 1 )
			ENDIF
		ENDIF


	! »»» ANTRIEBSKÖRPER ausgeben

	GOSUB "calcBody"   ! Geometrie berechnen und Transformation

	IF NOT(stand AND einszwei = 2) THEN
	! IF NOT(stand) OR (einszwei # 2) THEN  !<-- identisch?
		IF n THEN
			GOSUB "KoerperED_"+STR(dk_LOD,3,0)
			IF dk_LOD > 350 THEN GOSUB "Fangpunkte"
			ENDIF
		ENDIF

 	IF n THEN DEL n


	! »»» SCHIENE / HALTEPUNKT ausgeben

	GOSUB "calcAncorPoint"   ! Geometrie berechnen und Transformation

	IF n THEN
		IF schere THEN
			zSS = z + dsz  ! Höhenlage Bewegungsebene Spannschloss
			GOSUB "Scherenanker"
			ELSE
			GOSUB "Gleitschiene"
			ENDIF
		DEL n
		ENDIF


 	! »»» MECHANIK ausgeben
	IF schere THEN
		GOSUB "positionS"
		xSS = x : ySS = y   ! Verbindungspunkt merken
		IF ABS(xSS)>tlr AND ABS(ySS)>tlr THEN GOSUB "Scherengestaenge_"+STR(dk_LOD,3,0)
		ELSE
		GOSUB "positionG"
		GOSUB "Gleithebel_"+STR(dk_LOD,3,0)
		ENDIF

	IF develop OR dk_LOD > 400 THEN
		htspt=htspt+1 : HOTSPOT DAx, DAy, zGE, htspt
		htspt=htspt+1 : HOTSPOT APx, APy, zGE, htspt
		ENDIF

	LINE_TYPE 1
	RETURN


! ------------------------------------------------------------------------- [ SUBS Schließkörper ]

"KoerperED_350":
"KoerperED_400":
	z = kz
	PUT	0,0,msk,
		kx, 0, msk,
		kx, ky, msk,
		0, ky, msk,
		0, 0, -1
	IF nsp > 9 THEN PRISM_ nsp/3, z, GET(nsp)
	RETURN

"KoerperED_500":
	! Körper der XEA-Serie
	
	n = n + 2
	sts = 1 - 2*(ac_OpeningSide ="R")
	sts = sts * (1 - 2*(montageED=2))
	sts = sts * (1 - 2*stand)
	IF sts < 0 THEN
		ROTx 90 : MULz -1 : ADD 0, kz/2, ky : MULx -1
		ELSE
		ROTx 90 : MULz -1 : ADD kx, kz/2, ky : MULx 1
		ENDIF
	CALL "dormakaba Logo XEA" PARAMETERS x = 0.035, y = 0.02, t = 0.001, r = 0.003
	MATERIAL dk_materials[dka_automation_mat]

	DEL 4

	ROTz 90 : ROTx 90
	z = kx : y = kz : x = ky : r = 0.0052
	GOSUB "roundHalfRect"
	IF nsp > 9 THEN PRISM_ nsp/3, z, GET(nsp)
	DEL 2

	RETURN


! ------------------------------------------------------------------------- [ SUBS Gegenstück ]

"Gleitschiene":
	IF dk_LOD> 400 THEN
		ROTz 90 : ROTx 90
		n = n + 2
		z = sx : y = sz : x = sy : r = 0.0052
		GOSUB "roundHalfRect"
		ELSE
		z = sz
		PUT  0,  0, msk,
			sx,  0, msk,
			sx, sy, msk,
			 0, sy, msk,
			 0,  0,  -1
		ENDIF

	IF nsp > 9 THEN PRISM_ nsp/3, z, GET(nsp)

	RETURN

"Scherenanker":
	s = 0.004
	r = sy-yL
	z = sz
	PUT -dsx,     s, msk,
		-dsx,     0, msk,
		 sx-dsx,  0, msk,
		 sx-dsx,  s, msk,
		 r,      sy, msk,
		-r,      sy, msk,
		-dsx,     s,  -1
	IF nsp > 9 THEN PRISM_ nsp/3, z, GET(nsp)
	RETURN


! ------------------------------------------------------------------------- [ SUBS Verbindungsmechanik ]

"Gleithebel_350":
"Gleithebel_400":
	htspt=htspt+1 : HOTSPOT x, y, zGE, htspt
	LIN_ DAx, DAy, zGE, x, y, zGE
	RETURN

"Gleithebel_500":
	htspt=htspt+1 : HOTSPOT x, y, zGE, htspt
	! Auf X ausrichten
	x = x - DAx : y = y - DAy
	IF ABS(x)<tlr THEN w = 0 ELSE w =ATN(y/x) + 180 * (x<0)
	! Ausgabe
	ADD DAx, DAy, zGE - zH/2
	ROTz w
	PRISM_ 5, zH,
		xH, -yH/2, msk2,
		 0, -yH/2, msk2,
		yH/2,-180, 2000+msk2,
		xH, yH/2, msk2,
		yH/2,-180, 2000+msk2

	ADD xH,0,-zG
	CYLIND zG, 0.0075
	DEL 3
	RETURN

"Scherengestaenge_350":
"Scherengestaenge_400":
	htspt=htspt+1 : HOTSPOT x, y, zGE, htspt
	LIN_ DAx, DAy, zGE,  xSS, ySS, zGE
	LIN_ xSS, ySS, zGE,  xSS, ySS, zSS
	LIN_ xSS, ySS, zSS,  APx, APy, zSS
	RETURN

"Scherengestaenge_500":
	htspt=htspt+1 : HOTSPOT x, y, zGE, htspt

	! »»» Spannschloss
	! Auf X ausrichten
	x = xSS - APx : y = ySS - APy
	IF ABS(x)<tlr THEN
		w = 90 + 180 * (y<0)
		ELSE
		w =ATN(y/x) + 180 * (x<0)
		ENDIF
	! Ausgabe
	ADD APx, APy, zSS
	ROTz w
	r= SQR ((APx - xSS)^2 + (APy - ySS)^2)
	ROTy 90
	CYLIND r, 0.007/2
	DEL 1
	ADDz -0.015/2
	PRISM_ 9, 0.015,
		r,       -yH/2, msk,
		r-0.10,  -yH/2, msk,
		r-0.12, -0.0075, msk,
		r-0.14, -0.0075, msk,
		r-0.14,  0.0075, msk,
		r-0.12,  0.0075, msk,
		r-0.10,   yH/2, msk,
		r,        yH/2, msk2,
		yH/2,-180, 2000+msk2
	DEL 3

	! »»» Gestänge
	! Auf X ausrichten
	x = xSS - DAx : y = ySS - DAy
	IF ABS(x)<tlr THEN w = 0 ELSE w =ATN(y/x) + 180 * (x<0)
	! Ausgabe
	ADD DAx, DAy, zGE - zH/2
	ROTz w
	! r= SQR ((DAx - xSS)^2 + (DAy - ySS)^2)
	PRISM_ 5, zH,
		xH, -yH/2, msk2,
		 0, -yH/2, msk2,
		yH/2,-180, 2000+msk2,
		xH, yH/2, msk2,
		yH/2,-180, 2000+msk2
	s = (zGE - zH/2) - (zSS + 0.015/2)
	ADD xh, 0, -s
	CYLIND s, yH/2.25
	DEL 3
	RETURN


! ------------------------------------------------------------------------- [ SUBS Diverses ]

"Fangpunkte":
	htspt=htspt+1 : HOTSPOT  0, ky, 0, htspt
	htspt=htspt+1 : HOTSPOT kx, ky, 0, htspt
	htspt=htspt+1 : HOTSPOT  0, ky, kz, htspt
	htspt=htspt+1 : HOTSPOT kx, ky, kz, htspt
	RETURN

"roundRect":
	PUT	0,		r, msk2,
		0,		-y, 800+msk2,
		r,		0, 1000+msk2,
		x-r,	0, msk2,
		x,		r, 1000+msk2,
		x,		y-r, msk2,
		x-r,	y, 1000+msk2,
		r,		y, msk2,
		0,		y-r, 1000+msk2,
		0,		r, -1

		! --> plus innen
		! r,		t, msk,
		! x-r,	t, msk,
		! x-t,	r, 1000+msk,
		! x-t,	y-r, msk,
		! x-r,	y-t, 1000+msk,
		! r,		y-t, msk,
		! t,		y-r, 1000+msk,
		! t,		r, msk,
		! r,		t, 1000+msk,
		! r,		t, -1
	RETURN

"roundHalfRect":
	PUT	0,		0, msk2,
		x-r,	0, msk2,
		x,		r, 1000+msk2,
		x,		y-r, msk2,
		x-r,	y, 1000+msk2,
		0,		y, msk2,
		0,		0, -1
	RETURN
